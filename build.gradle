// Top-level build file where you can add configuration options common to all sub-projects/modules.

def appProps = new Properties()
def appPropsFile = file("app.properties")
if (appPropsFile.exists()) {
    appPropsFile.withInputStream { appProps.load(it) }
}

ext {
    compileSdkVersion = (appProps.getProperty("COMPILE_SDK", "35") as String).toInteger()
    minSdkVersion = (appProps.getProperty("MIN_SDK", "23") as String).toInteger()
    ndkVersion = appProps.getProperty("NDK_VERSION", "28.1.13356709") as String
}

buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.aliyun.com/repository/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url 'https://maven.aliyun.com/repository/public' }
    }
}

// c++配置 生成缓存目录清理
task externalNativeBuildClean(type: Delete) {
    rootProject.subprojects {
        def cxxCachePath = it.file(".cxx")
        if (cxxCachePath.exists()) {
            delete cxxCachePath
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
